// Simple (and rather pointless) example of how to implement a custom modopen()
// function. lib.pla is a more realistic example, although note that you can
// load this as well as lib.pla to see what's going on.

include "inc/cmdsys.plh"
include "inc/cmdsysac.plh"

// There's no requirement to chain modopen() functions, but let's demonstrate
// that it's possible if desired.
word parent_modopen

def putcr(cr)#0
    word i
    i = cr
    while ^i <> 13
        putc(^i)
        i++
    loop
end

def my_modopen(modcr)#1
    byte handle
    puts("my_modopen@"); puth(@my_modopen)
    puts(" - about to call parent, modcr: "); putcr(modcr); putln()
    handle = parent_modopen(modcr)#1
    puts("my_modopen@"); puth(@my_modopen)
    puts(" - after calling parent, modcr: "); putcr(modcr)
    puts(", handle: "); puti(handle); putln()
    return handle
end

// This is very simplistic and doesn't try to prevent itself being installed
// multiple times. This doesn't matter too much as it's just a simple example,
// and this is also potentially helpful if trying to debug another modopen()
// hook.
parent_modopen = modopen
modopen = @my_modopen
puts("Installed my_modopen()\n")
return modkeep
done
