// TODONOW: Turn the cursor off during the animation and on afterwards

// TODONOW: Use graphics not Xs

// TODONOW: Label the three pegs A, B and C

// TODONOW: Can I draw a "frame" for each tower? (In white, say)

// TODONOW: Allow 6 discs?

include "inc/cmdsys.plh"
include "inc/testlib.plh"
include "inc/acornos.plh"

const discs = 5 // 1..n, we use 0..n-1 internally
const animate = 1

word height[3]

asm equates
    BBC = 1
    !SOURCE "vmsrc/plvmzp.inc"
end

asm sync
    DEX
    STX	ESP
    LDA #19
    JSR $FFF4
    LDX ESP
    RTS
end

def time(t)
    call(1, t, t >> 8, 0, $FFF1)
end

def sleep(cs)
    byte time1[5], time2[5]
    word delta
    time(@time1)
    repeat
	time(@time2)
	delta = time2[0] - time1[0]
	if delta < 0
	    delta = delta + 256
	fin
    until delta >= cs
end

def peg_x(peg)
    return 3 + 12 * peg
end

def height_y(n)
    return 20 - n
end

def peg_y(peg)
    return height_y(height[peg])
end

def tab(x, y)
    putc(31)
    putc(x)
    putc(y)
end

def plot_peg_line()
    putc(151)
    puts("     ")
    putc(53)
    puts("     ")
end

def plot_peg(peg)
    word x, y, i
    x = peg_x(peg)
    y = height_y(0) - 1

    tab(x + 6, y + 2)
    putc('A' + peg)

    tab(x, y + 1)
    putc(151)
    for i = 0 to 9 
	putc(96)
    next
    putc(33)
    
    while y >= height_y(discs) - 1
	tab(x, y)
	plot_peg_line()
	y--
    loop
end

def plot_disc(disc, x, y)
    word i
    tab(x + (discs - 1 - disc), y)
    putc(145 + disc)
    for i = 0 to disc
	putc(255)
	putc(255)
    next
    putc(53)
end

def erase_disc(x, y)
    if y >= height_y(discs) - 1
	tab(x, y)
	sync()
	plot_peg_line()
    else
	tab(x + 1, y)
	sync()
	puts("           ")
    fin
end

def add_disc(disc, peg)
    word i
    height[peg]++
    plot_disc(disc, peg_x(peg), peg_y(peg)))
end

def remove_disc(disc, peg)
    word x, y
    x = peg_x(peg)
    y = peg_y(peg)
    tab(x, y)
    erase_disc(x, y)
    height[peg]--
end

def min(a, b)
    if a < b
	return a
    else
	return b
    fin
end

def max(a, b)
    if a < b
	return b
    else
	return a
    fin
end

def move_disc_animated(disc, source, dest)
    word i, min_y, y, x, dest_x, dx, dest_y

    height[dest]++

    min_y = height_y(discs) - 3

    x = peg_x(source)
    y = peg_y(source)
    while y > min_y
	erase_disc(x, y)
	y--
	plot_disc(disc, x, y)
	sleep(5)
    loop
    height[source]--

    dest_x = peg_x(dest)
    if dest_x > x
	dx = 1
    else
	dx = -1
    fin
    while x <> dest_x
	erase_disc(x, y)
	x = x + dx
	plot_disc(disc, x, y)
	sleep(5)
    loop

    dest_y = peg_y(dest)
    while y < dest_y
	erase_disc(x, y)
	y++
	plot_disc(disc, x, y)
	sleep(5)
    loop
end

def move_disc(disc, source, dest)
    tab(0, 0)
    puts("Move disc ")
    puti(disc + 1)
    puts(" from peg ")
    putc('A' + source)
    puts(" to peg ")
    putc('A' + dest)
    puts("...")
    if animate
	move_disc_animated(disc, source, dest)
    else
	remove_disc(disc, source)
	add_disc(disc, dest)
	sleep(100)
    fin
end

def move_tower(disc, source, dest, spare)
    if disc == 0
	move_disc(disc, source, dest)
    else
	move_tower(disc - 1, source, spare, dest)
	move_disc(disc, source, dest)
	move_tower(disc - 1, spare, dest, source)
    fin
end

def main()
    word i
    mode(7)
    for i = 0 to 2
	height[i] = 0
	plot_peg(i)
    next
    for i = discs - 1 downto 0
	add_disc(i, 0)
    next
    move_tower(discs - 1, 0, 1, 2)
    tab(0, 21)
end

main()
done

// vi: sts=4 sw=4
