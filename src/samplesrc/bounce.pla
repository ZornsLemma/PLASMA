// This is a manual translation of David Given's COWGOL bounce.cow program.
// (https://github.com/davidgiven/cowgol/blob/592433a07ca03d8235b1364da2286e9b097e7e79/demo/bounce.cow)
// 
// Copyright (c) 2015 David Given
// All rights reserved.
// 
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
// 
// 1. Redistributions of source code must retain the above copyright notice,
//    this list of conditions and the following disclaimer.
// 
// 2. Redistributions in binary form must reproduce the above copyright notice,
//    this list of conditions and the following disclaimer in the documentation
//    and/or other materials provided with the distribution.
//    
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
// AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
// IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
// ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
// LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
// CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
// SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
// INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
// CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
// ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
// POSSIBILITY OF SUCH DAMAGE.

include "inc/acornc.plh"
include "inc/cmdsys.plh"
include "inc/cmdsysac.plh"

const screen = $7C00
const ball_count = 10

struc t_ball
    byte ball_x
    byte ball_y
    byte ball_dx
    byte ball_dy
end

byte balls[ball_count * t_ball]

def setup_screen()#0
    word ptr
    byte line
    ptr = screen
    for line = 0 to 24
        ^ptr = 147
        ptr = ptr + 40
    next
end

def vsync()#0
    call(osbyte, osbyte_wait_vertical_retrace, 0, 0, 0)
end

def putpixel(x, y, ink)#0
    word xx, yy, ptr
    byte p
    xx = x / 2
    yy = y / 3
    ptr = screen + 1 + (yy*40) + xx
    x = x & 1
    y = y % 3
    p = 1 << ((y*2) + x)
    p = (p | ((p & 32)<<1)) & ~32
    ^ptr = ^ptr & ~p | (p & ink) | 160
end

def draw_ball(ball, ink)#0
    byte x, x1, x3
    byte y, y1, y3
    byte z
    x = ball->ball_x
    x1 = x+1
    x3 = x+3
    y = ball->ball_y
    y1 = y+1
    y3 = y+3
    for z = 1 to 2
        putpixel(x+z, y, ink)
        putpixel(x+z, y3, ink)
        putpixel(x, y+z, ink)
        putpixel(x3, y+z, ink)
    next
end

def init_balls()#0
    word ball
    byte i
    i = 0
    while 1
        ball = @balls + i*t_ball
        ball->ball_x = i<<2
        ball->ball_y = i<<2
        ball->ball_dx = (i % 3) - 1
        ball->ball_dy = ((i / 3) % 3) - 1
        if i == ball_count-1
            break
        fin
        i = i + 1
    loop
end
        
def draw_balls(ink)#0
    word ball
    byte i
    i = 0
    while 1
        ball = @balls + i*t_ball
        draw_ball(ball, ink)
        if i == ball_count-1
            break
        fin
        i = i + 1
    loop
end

def move_balls()#0
    word ball
    byte i
    i = 0
    while 1
        ball = @balls + i*t_ball
        if ball->ball_x == 0
            ball->ball_dx = 1
        elsif ball->ball_x == 73
            ball->ball_dx = -1
        fin
        if ball->ball_y == 0
            ball->ball_dy = 1
        elsif ball->ball_y == 70
            ball->ball_dy = -1
        fin

        ball->ball_x = ball->ball_x + ball->ball_dx
        ball->ball_y = ball->ball_y + ball->ball_dy

        if i == ball_count-1
            break
        fin
        i = i + 1
    loop
end

mode(7)
vsync()
setup_screen()
init_balls()

while 1
    draw_balls($00)
    move_balls()
    draw_balls($FF)
    vsync()
loop

done
