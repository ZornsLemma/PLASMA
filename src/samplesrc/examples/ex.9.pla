//
// THIS IS A FOREST FIRE SIMULATION.
//
// WRITTEN IS A SLIGHTLY DIFFERENT STYLE.
//
CONST FALSE      = 0
CONST TRUE       = NOT FALSE
CONST SHOWLORES  = $C056
CONST KEYBOARD   = $C000
CONST KEYSTROBE  = $C010
CONST EMPTY      = 0
CONST TREE       = 4
CONST FIRE       = 13
CONST FORESTSIZE = 42*42
CONST RNDNUM     = $4E
CONST RNDL       = $4E
CONST RNDH       = $4F
BYTE  TREES1[FORESTSIZE] // ALT ARRAY SIZE SPECIFIER
BYTE  TREES2[FORESTSIZE]

DEF TEXTMODE
  CALL($FB39, 0, 0, 0, 0)
  RETURN HOME
END

DEF GRMODE
  CALL($FB40, 0, 0, 0, 0)
  ^SHOWLORES
  RETURN HOME
END

//
// RANDOM NUMBERS
//
DEF RND
  *RNDNUM = (*RNDNUM << 1) + *RNDNUM + 251
  RETURN *RNDNUM & $7FFF
END

//
// CHECK IF A FIRE IS BURNING AROUND TREE
//
DEF BYFIRE(TREEPTR)
  IF    ^(TREEPTR - 43) == FIRE
    RETURN TRUE
  ELSIF ^(TREEPTR - 42) == FIRE
    RETURN TRUE
  ELSIF ^(TREEPTR - 41) == FIRE
    RETURN TRUE
  ELSIF ^(TREEPTR - 1)  == FIRE
    RETURN TRUE
  ELSIF ^(TREEPTR + 1)  == FIRE
    RETURN TRUE
  ELSIF ^(TREEPTR + 41) == FIRE
    RETURN TRUE
  ELSIF ^(TREEPTR + 42) == FIRE
    RETURN TRUE
  ELSIF ^(TREEPTR + 43) == FIRE
    RETURN TRUE
  FIN
  RETURN FALSE
END

DEF FORESTFIRE
  WORD NEWTREES, OLDTREES, NEWTREE, OLDTREE, YROW
  BYTE X, Y

  MEMSET(@TREES1, EMPTY, FORESTSIZE)
  MEMSET(@TREES2, EMPTY, FORESTSIZE)
  OLDTREES = @TREES1
  NEWTREES = @TREES2

  FOR Y = 1 TO 40
    YROW = Y * 42
    FOR X = 1 TO 40
      IF RND < 8000
        ^(OLDTREES + X + YROW) = TREE
      FIN
    NEXT
  NEXT
  WHILE ^KEYBOARD < 128
    FOR Y = 1 TO 40
      YROW = Y * 42
      FOR X = 1 TO 40
        OLDTREE = OLDTREES + X + YROW
        NEWTREE = NEWTREES + X + YROW
        WHEN ^OLDTREE
          IS EMPTY
            IF RND < 5000
              ^NEWTREE = TREE
            ELSE
              ^NEWTREE = EMPTY
            FIN
            BREAK
          IS TREE
            IF RND < 5 OR BYFIRE(OLDTREE)
              ^NEWTREE = FIRE
            ELSE
              ^NEWTREE = TREE
            FIN
            BREAK
          IS FIRE
            ^NEWTREE = EMPTY
        WEND
        CALL($F864, ^NEWTREE, 0, 0, 0)
        CALL($F800, Y - 1, 0, X - 1, 0)
      NEXT
    NEXT
    YROW = NEWTREES
    NEWTREES = OLDTREES
    OLDTREES = YROW
  LOOP
  RETURN ^KEYSTROBE
END

PUTS("PRESS ANY KEY TO BEGIN...")
GETC
GRMODE
HOME
GOTOXY(10,2)
PUTS("PRESS ANY KEY TO EXIT.")
FORESTFIRE
TEXTMODE
HOME
PUTS("THAT'S ALL FOLKS!")
DONE
