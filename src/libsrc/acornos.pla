include "inc/cmdsys.plh"

const eof = -1
const osfind_addr = $FFCE

export asm oserror(n, msg)
    BBC = 1
    !SOURCE "vmsrc/plvmzp.inc"
    LDA ESTKL+1,X
    STA $101
    LDA ESTKL,X
    STA TMPL
    LDA ESTKH,X
    STA TMPH
    LDY #0
    LDA (TMP),Y
    TAY
    LDA #0
    STA $100 ; BRK opcode
    STA $102,Y
-   LDA (TMP),Y
    STA $101,Y
    DEY
    BNE -
    JMP $100
end

export def osfind_open(a, filename)
    // TODO: This will go wrong if the filename is too long
    byte cr[251]
    stocr(filename, @cr)
    return call(a, @cr, @cr >> 8, 0, osfind_addr).0
end

// TODO: It's *probably* slightly smaller and definitely a bit faster to
// hand-roll assembly wrappers instead of using call(); perhaps do this
// once the API has settled down.
export def osfind_close(handle)
    call(0, 0, handle, 0, osfind_addr)
end

// TODO: Might be worth coding this in assembly for performance
export def osbget(handle)
    word result
    result = call(0, 0, handle, 0, $FFD7)
    if result->3 & 1
	return eof
    else
	return result->0
    fin
end

export def osrdch()
    return call(0, 0, 0, 0, $FFE0).0
end

// vi: sts=4 sw=4
